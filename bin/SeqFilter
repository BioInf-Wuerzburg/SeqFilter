#!/usr/bin/env perl

use warnings;
use strict;

use Getopt::Long;
use Pod::Usage;

use FindBin qw($RealBin);
use lib "$RealBin/../lib/";
use File::Basename;

use Verbose;
use Verbose::ProgressBar;

use Fastq::Parser 0.09;
use Fastq::Seq 0.11;

use Fasta::Parser 0.08;
use Fasta::Seq 0.06;

our $VERSION = '1.05';

=head1 NAME

SeqFilter - Versatile FASTA/FASTQ sequence file analysis and modification tool.

=head1 CHANGELOG

see git log

=head1 SYNOPSIS

  perl SeqFilter --in <SEQFILE> <OPTIONS> --out <OUTFILE>
  
  cat <SEQFILE> | SeqFilter <OPTIONS> --out '-' > OUTFILE

=cut

=head1 OPTIONS

=cut

=over

=cut

my %opt;

=item [--in=<FASTA/FASTQ>]

Input FASTA/FASTQ file. Default STDIN.

=cut

$opt{'in=s'} = \(my $opt_in = '-');

=item [--out=<FILE/->]

Output file. Default off. Specify '-' for STDOUT.

=cut

$opt{'out=s'} = \(my $opt_out = undef);

=item [--tsv=<STRING/->]

Print tab separated statistics of data AFTER filtering to pathname, 
 default off, specify '-' for STDOUT, but only if '--out' is not redirected 
 to STDOUT.

Column order is: number of sequences, total length, longest, shortest, 
 Nx-values, base-contents. No header is printed, so multiple runs can 
 simply be concatenated.

=cut 

$opt{'tsv=s'} = \(my $opt_tsv = '');

=item [--ids=<FILE/-/IDLIST>]

Pathname to file of sequence IDs or a literal list of IDs to be reported. 
 Reads comma-separated, whitespace and newline separated lists. Specify '-' 
 to read IDs form STDIN. Leading '>' or '@' are allowed but not necessary.

  # get two seqs by ID
  SeqFilter fasta.fa --ids "seq35,seq49"
  # read ids from file
  SeqFilter fasta.fa --ids ids.list
  

=cut 

$opt{'ids=s'} = \(my $opt_ids = '');

=item [--ids-exclude]

Exclude reads specified by --ids from being reported. Default off.

=cut 

$opt{'ids-exclude'} = \(my $opt_ids_exclude = 0);

=item [--ids-pattern=<FILE/-/PATTERNLIST>]

A PATTERN or a link to a file containing multiple PATTERN, one per 
 line, to match against sequence ids. Only matching sequences will
 be returned.

  # get seqs with ids matching pattern
  SeqFilter seqs.fq --ids-patt 'comp2_c13_seq*|comp2_c88_seq*' 

=cut

$opt{'ids-pattern=s'} = \(my $opt_ids_pattern = undef);

=item [--ids-split]

Extend use of --ids-pattern from only identifying sequences to also splitting
 them into different files according to a REQUIRED first capture group
 in the match. If capture group is empty, sequences are written to --out. 
 If multiple pattern are provided and an ID has multiple matches, all but 
 the first match are ignored.

  # split seqs by library (LIB1, LIB2, LIB14)
  SeqFilter multilib.fq --ids-pattern '\w+(\d+)' --split-ids
  # creates multilib1.fq, multilib2.fq, multilib14.fq

NOTE: Perl needs to open a filehandle to every split file, this can slow 
 things down considerably if you want to split into more than 1000 different
 files with occurances of patterns randomly mixed in source file.

=cut

$opt{'ids-split'} = \(my $opt_ids_split = undef);

=item [--ids-rename=<PATTERN>]

Provide a perl substitution pattern as string. The pattern is applied to 
 every id. Use global "$COUNT" to access the output sequence counter, 
 $PICARD to access the picard number.

  SeqFilter library_1.fq --ids-rename='s/.*\//sprintf("MYLIB_%05d\/%d", $COUNT, $PICARD)/e'
  # creates ids:
  # @MYLIB_00001/1
  # @MYLIB_00002/1 ...

=cut

$opt{'ids-rename=s'} = \(my $opt_ids_rename = undef);

=item [--desc-replace]

Replace description from sequence header in output. Without argument
removes entire description.

=cut

$opt{'desc-replace:s'} = \(my $opt_desc_replace = undef);

=item [--desc-append]

Append description of sequence header in output.

=cut

$opt{'desc-append=s'} = \(my $opt_desc_append = undef);

=item [--min-length=<INT>]

Minimum sequence length.

=cut

$opt{'min-length=i'} = \(my $opt_min_length = 0);

=item [--max-length=<INT>]

Maximum length for sequences to be retrieved. Default off.

=cut

$opt{'max-length=i'} = \(my $opt_max_length = undef);

=item [--fasta]

FASTQ only. output FASTA format. Default FASTQ.

=cut

$opt{'fasta'} = \(my $opt_fasta = undef);

=item [--line-width] [0]

Line width for FASTA sequences. Set 0 for single line.

=cut

$opt{'line-width=i'} = \(my $opt_line_width = undef);

=item [--rev-comp=<FILE/-/LIST/*>]

Pathname to file of sequence IDs or list of IDs to be transformed, "*" to 
 transform all sequences. Formatting follows the same rules as --ids files. 

=cut 

$opt{'rev-comp=s'} = \(my $opt_rc = '');

=item [--check-format]

FASTQ only. Check every FASTQ record to have a valid format. '@/+' at the
 start of id lines, sequence and quality of identical length, phred within
 boundaries. Slows down the parsing.

=cut

$opt{'check-format!'} = \(my $opt_check_format = undef);

=item [--phred-offset] [auto]

FASTQ only. Phred offset for quality scores. Default auto-detect.

=cut

$opt{'phred-offset=i'} = \(my $opt_offset);

=item [--phred-transform]

FASTQ only. Transform phreds from input offset to specified C<--phred-transform> offset,
 usually 33 to 64 or wise versa.

=cut

$opt{'phred-transform'} = \(my $opt_transform);

=item [--phred-mask]

FASTQ only. Two values separated by ",", e.g "0,10" to mask all Nucleotides with
 phred below 10 with an "N".
 
NOTE: Requires C<--phred-offset>.

=cut

$opt{'phred-mask=s'} = \(my $opt_mask = undef);

=item [--trim-window=<INT1>,[<INT2>]]

FASTQ only. Trim sequences to quality >= INT1 using a sliding window of size
 INT2, default 10. The sliding window allows to have positions
 below the <--trim> cutoff provided the window mean is higher than
 INT1. It is made sure that a) positions with quality below cutoff only 
 occur within the remaining sequence, not at its start/end and b) windows
 never overlap eachother.

=cut

$opt{'trim-window=s'} = \(my $opt_trim = undef);

=item [--trim-lcs=<INT,INT,INT>]

FASTQ only. Three values separated by ",", e.g. "30,40,50" to grep all stretches
 of quality >= 30 and minimum length 50 from the sequences.
Faster than C<--trim-window> yet breaks sequences even on a single low
 quality position.

NOTE: C<--trim-lcs> and C<--trim-window> can be combined, e.g.

  --trim-lcs 5,40,100 --trim-window 10

will generate sequences with qualities of at least 5 at every position and 
 a window mean of 10.

=cut

$opt{'trim-lcs=s'} = \(my $opt_slice_lcs = undef);

=item [--substr=<FILE/-/LIST>]    

Pathname to a FILE containing information for subseq extraction/modification.
The format is a tsv, by default lines of the format ID FROM TO are expected.
Lines prepened by '#' are treated as comments and therefore ignored.
If --substr-perl-style is set, the lines must start with the ID of the read,
 followed by the substr
 values OFFSET,LENGTH,REPLACESEQ,REPLACEQUAL. The parameter usage is than the 
 same as for perl builtin C<substr> function, meaning an OFFSET alone is 
 sufficient, a positive value is set from the start of the sequence, a 
 negative offset from the end, without LENGTH, the sequence is returned from
 OFFSET to its end. REPLACEMENTS are introduced at the OFFSET position, 
 if LENGTH is 0, it is a simple insertion, else a part is deleted first and
 the REPLACEMENT is then inserted. Substring extraction is of course 
 performed prior to any other trimming. To trim all reads use '*' instead
 of the read id. This command will be performed prior to any indiviual 
 substr command.

  FROM TO:
    # extract sequence from pos 10 to pos 50
    read1 10 50
  
  OFFSET [LENGTH [REPLACEMENT]]
    # trim read1 head and tail by 10
    read1   10
    # extract from read2 250 nts starting at pos 15
    read2   15   250
    # replace 3 nt by an "N"" with qual "!" (for FASTQ)
    read3   3   1   N   !
    # trim from all reads 5nts at the beginning and the end.
    *	5
    *	-5

=cut

$opt{'substr=s'} = \(my $opt_substr_file = undef);

=item [--substr-perl-style]

By default, substr information are read according to the format FROM TO.
Set this flag to switch the behaviour to perl substr() like style of 
"OFFSET [LENGTH [REPLACEMENT]]"

=cut

$opt{'substr-perl-style!'} = \(my $opt_substr_perl);

=item [--N=<INT,INT...>]

Report Nx value (N50, N90...). Specify multiple values as comma separated
 STRING. Takes filter settings into account. Result might be ommited if to
 few sequences are retrieved. Default "50,90".

=cut 

$opt{'N=s'} = \(my $opt_N = '90,50');

=item [--base-content=<BASE(S),BASE(S),BASE(S),...>]

Count and print the relative amount of given bases. Takes a "," separated 
 list, each element of the list can be one or more bases. In the letter case
 the commulative amount of the individual bases is calculated.
 
Examples:
  --base-content=A,T,G,C,N   # content of regular bases
  --base-content=GC          # combined GC content

=cut

$opt{'base-content=s'} = \(my $opt_base_content = undef);

=item --verbose=<INT>

Toggle verbose level, default 2, which outputs statistics and progress.
Set 1 for statistics only or 0 for no verbose output.

=cut 

$opt{'verbose=i'} = \(my $opt_verbose = 2);

=item --quiet

Omit all verbose messages. The same as --verbose=0, Superceeds --verbose settings.

=cut

$opt{'quiet'} = \(my $opt_quiet);

=item [--help]

Display this help

=cut

$opt{'help|?'} = \(my $opt_help);

=item [--version|-V]

Display current version

=cut

$opt{'version|V'} = \(my $opt_version);



=back

=cut



GetOptions(%opt) or pod2usage(1);
if(@ARGV && !($ARGV[0] =~ /^-/)){
	$opt_in = $ARGV[0];
}

pod2usage(1) if $opt_help;
if ($opt_version){
	print "$VERSION\n";
	exit(1);
}

if($opt_max_length && $opt_min_length && ($opt_min_length > $opt_max_length)){
	pod2usage(exitval=>1, msg=>'min-length has to be smaller than max-length'); 
}

$opt_verbose = 0 if $opt_quiet; 



##------------------------------------------------------------------------##

=head1 MAIN

=cut

my $V = Verbose->new(
	level => 1,
	report_level => $opt_verbose,
	line_width => 80,
	
);

$V->verbose("$0-$VERSION");


##------------------------------------------------------------------------##

=head2 check input file

=cut

# check STDIN, check files
# $opt_in, $opt_ids, $opt_ids_pattern, $opt_substr_file
# only one can be '-'
my $opt_stdin;


if($opt_in ne '-'){
	$V->exit("Cannot find input file: $opt_in") if ! -f $opt_in;
	$V->exit("Empty input file: $opt_in") if -s $opt_in == 0;
}else{
	$opt_stdin='--in';
}

# Fasta parser
$V->verbose("--in: ".( $opt_in ne '-' ? $opt_in : "STDIN" ));

my $FP;
my $FASTQ;

if(
	$FP = Fastq::Parser->new(
		file => $opt_in eq '-' ? undef : $opt_in, # defaults to STDIN if undef
	)->check_format
){
	$FASTQ = 1;
	if($opt_offset){
		$FP->phred_offset($opt_offset);
	}else{
		$opt_offset = $FP->guess_phred_offset 
	};
	pod2usage(msg=>'Could not guess phred offset, please specify manually', exitval => 2) 
		unless $opt_offset;
	$V->verbose('Detected FASTQ format, phred-offset '.$opt_offset);
}elsif(
	$FP = Fasta::Parser->new(
		file => $opt_in eq '-' ? undef : $opt_in, # defaults to STDIN if undef
	)->check_format
){
	$FASTQ = 0;
	$V->verbose('Detected FASTA format');
}else{
	$V->exit(( $opt_in ? $opt_in : "STDIN" )." neither FASTA nor FASTQ") unless $FP;
}

# a fix to handle gzipped files, -s doesnt work on gzip 
my $file_size = $FP->can('file_size') ? $FP->file_size : -s $FP->fh; 

my $VPB = Verbose::ProgressBar->new(
	level => 2,
	report_level => $opt_verbose,
	size => $file_size, # size and _fh required for gzip fix
	_fh => $FP->fh(),
);


# output file
my $ofh;
if($opt_out){
	if($opt_out eq '-'){
		$ofh = \*STDOUT;
	}else{
		open($ofh, ">", $opt_out) || $V->exit("Can't open output read file: '$opt_out'");
	} 
}


##------------------------------------------------------------------------##

=head2 FASTQ/FASTA parameter

=cut

my %opt_fq_only = (
	'phred-transform' => $opt_transform,
	'phred-mask' => $opt_mask,
	'trim-window' => $opt_trim,
	'trim-lcs' => $opt_slice_lcs,
	'check-format' => $opt_check_format,
);

if(!$FASTQ){
	while (my ($k,$v) = each %opt_fq_only){
		defined $v && $V->exit("$k can only be used on FASTQ data"); 
	}
}

my %opt_fa_only = (
	'line-width' => $opt_line_width,
);

if($FASTQ){
    $V->exit('--line-width only supported for FASTA output') if ($FASTQ && !$opt_fasta && $opt_line_width);
}


##------------------------------------------------------------------------##

=head2 Maybe files parameter

=cut

my ($opt_ids_fh, $opt_rc_fh, $opt_ids_pattern_fh, $opt_substr_fh);
($opt_ids_fh, $opt_stdin) = opt2handle('--ids',$opt_ids, $opt_stdin);
($opt_ids_pattern_fh, $opt_stdin) = opt2handle('--ids-pattern',$opt_ids_pattern, $opt_stdin);
($opt_substr_fh, $opt_stdin) = opt2handle('--substr-file', $opt_substr_file, $opt_stdin);


my %IDS = read_ids($opt_ids_fh) if $opt_ids;

# store filehandles for split pattern
my %IDS_SPLIT;
my $IDS_SPLIT_OPEN = 0;
my $IDS_SPLIT_MATCH;

my ($IN_BASE, $IN_PATH, $IN_SUFFIX) = ($opt_stdin && $opt_stdin eq '--in') ? ('', '', '') : fileparse($opt_in, qw(.fa .fasta .fq .fastq .FA .FASTA .FQ .FASTQ) );
unless($IN_SUFFIX){
	$IN_SUFFIX = $FASTQ ? '.fq' :  '.fa';
}


my %RC_IDS;
my $RC_ALL;
if($opt_rc){
	my $opt_rc_fh;
	if($opt_rc eq '*'){
		$RC_ALL++;
	}elsif($opt_rc eq '-' && $opt_ids eq '-'){
		if($opt_ids_exclude){
			$V->exit("It does not make sense to rev-comp the same ids you are going to exclude!");
		}
		%RC_IDS = %IDS;
	}elsif(-f $opt_rc){
		open($opt_rc_fh, '<', $opt_rc) or $V->exit("$!: $opt_rc");
	}else{
		open($opt_rc_fh, '<', \$opt_rc) or $V->exit("$!: $opt_rc");
	}
	%RC_IDS = read_ids($opt_rc_fh) if $opt_rc_fh;
}


(my @IDS_PATTERS = <$opt_ids_pattern_fh>) if $opt_ids_pattern;
chomp(@IDS_PATTERS);
my %SUBSTR = read_substrs($opt_substr_fh) if $opt_substr_file;

##------------------------------------------------------------------------##

=head2 prepare som filter

=cut

my @opt_base_content = split(/\s*,\s*/, $opt_base_content) if $opt_base_content;
my @opt_N = sort{$a <=> $b}(split(/\s*,\s*/, $opt_N)) if $opt_N;

my $tsv;
if($opt_tsv){
	if($opt_tsv eq '-' && (!defined($opt_out) || $opt_out ne '-')){
		$tsv = \*STDOUT;
	}else{
		open($tsv, ">", $opt_tsv) || $V->exit("Can't open tsv stats file: '$opt_tsv'");
	} 
}

if($opt_mask){
	my($from, $to, $length) = split(',', $opt_mask);
	Fastq::Seq->Qual_low_range($from, $to, $opt_offset);
}

if($opt_slice_lcs){
	my($from, $to, $length) = split('\s*,\s*', $opt_slice_lcs);
	Fastq::Seq->Qual_lcs_range($from, $to, $opt_offset);
	Fastq::Seq->Qual_lcs_min_length($length);
}

if($opt_trim){
	my ($min, $wsize) = (split(',', $opt_trim), 10);
	Fastq::Seq->Qual_window_min($min);
	Fastq::Seq->Qual_window_size($wsize);
}

if($opt_check_format){
	Fastq::Seq->CheckFormat(1);
}

# TOTAL
my @total_L;
my $total_count;	# total count
my $total_length = 0;
my $total_longest = 0;
my $total_shortest = undef;
my %total_Ns;

# FILTERED
my @filtered_L; # lengths of seqs
my $COUNT = 0;
my $filtered_length = 0;
my $filtered_longest = 0;
my $filtered_shortest = undef;
my %filtered_Ns; #Nx values
my %BCs; # base contents



##------------------------------------------------------------------------##

=head2 loop through file and apply filter

=cut

# loop through FASTQ
LOOP: while (my $SEQ = $FP->next_seq()){
	$total_count++;
	$VPB->update();
	
	my $ID = $SEQ->id;
	my $length = length($SEQ->seq);
	
	# TOTAL STATS		
	# store length for Nx computation
	push @total_L, $length;
	# length stuff
	$total_length+=$length;
	$total_longest = $length if $length > $total_longest;
	$total_shortest = $length if (!defined ($total_shortest) || $length < $total_shortest);
	
	
	# FILTER
	# ids
	if(keys %IDS){
		if ($opt_ids_exclude){
			next if $IDS{$ID}
		}else{
			next unless $IDS{$ID}
		}
	}

	
	if(@IDS_PATTERS){
          my $matched = 0;
		foreach my $patt(@IDS_PATTERS){
                        $matched = 1 if $ID =~m/$patt/;
			if($opt_ids_split){
				next unless $1;
				$ofh = ids_split_fh($1);
			}
			last if $ID;
		}
                next LOOP if $matched == $opt_ids_exclude;
	}


	my @fq = ($SEQ);

	if($opt_substr_file){
	    my @tmp;
	    foreach my $x ('*',$ID){
		if(exists $SUBSTR{$x} ){
		    foreach my $fq (@fq){
			my @subs = $fq->substr_seq(@{$SUBSTR{$x}{coords}});
			for(my $i=0; $i<@subs; $i++){
			    $subs[$i]->reverse_complement() if $SUBSTR{$x}{rc}[$i]
			}
			push @tmp, @subs;
		    };

		}
	    }
	    @fq = @tmp;
	}

	# slice by lcs
	if($opt_slice_lcs){ # trim lcs
		@fq = map{$_->substr_qual_lcs(1)}@fq;
	}
	if($opt_trim){ # trim window
		@fq = map{
			my @wins = $_->qual_window(1);
			@wins ? $_->substr_seq(@wins) : ();
		}@fq;
	}
	
	foreach my $SEQ (@fq){
		my $length = length($SEQ->seq);
		# min-length/max-length
		if($opt_min_length){ next if $length < $opt_min_length }; 
		if($opt_max_length){ next if $length > $opt_max_length };

		# 
		$COUNT++;
		
		# rename ids
		if($opt_ids_rename){
			my $id = $SEQ->id();
			my $PICARD = '';
			if($FASTQ){
			    $PICARD = $SEQ->picard() || '';
			}
			eval '$id =~ '.$opt_ids_rename;
			$V->exit("Evaluation --ids-rename pattern failed. Make sure it is a valid perl substitution pattern\n".$@) if $@;
			$SEQ->id($id);
		}

		# reverse complement
		if($RC_ALL){
			$SEQ->reverse_complement;
		}elsif(keys %RC_IDS){
			$SEQ->reverse_complement if $RC_IDS{$ID};
		}
		
		# mask low quality
		$opt_mask && $SEQ->mask_qual_low();
		
		# OUTPUT
		if($ofh){
                    defined ($opt_desc_replace) && $SEQ->desc($opt_desc_replace);
                    $opt_desc_append && $SEQ->desc_append($opt_desc_append);
                    
                    if ($opt_fasta) {
                        print $ofh Fasta::Seq->new(
                                                   seq_head => substr ($SEQ->seq_head, 1),
                                                   seq => $SEQ->seq,
                                                  )->string($opt_line_width);
                    } elsif ($opt_transform) {
                        print $ofh $SEQ->phred_transform()->string();
                    } else {
                        print $ofh $SEQ->string($opt_line_width);
                    }
		}
		
		# OUTPUT STATS
		# store length for Nx computation
		push @filtered_L, $length;
		# length stuff
		$filtered_length+=$length;
		$filtered_longest = $length if $length > $filtered_longest;
		$filtered_shortest = $length if (!defined ($filtered_shortest) || $length < $filtered_shortest);
		# base content
		if(@opt_base_content){
			foreach (@opt_base_content){
				$BCs{$_} += $SEQ->base_content($_);
			}
		}
	}
}

#TODO: adjust to file_s
$VPB->finish($file_size);


=head2 print summary

=cut

#Nx computation
if(@opt_N){
	%filtered_Ns = Nx(total_length => $filtered_length, lengths => [@filtered_L], Nxs => [@opt_N]);
	%total_Ns = Nx(total_length => $total_length, lengths => [@total_L], Nxs => [@opt_N]);
}

$V->hline();
$V->verbose('Input');
$V->verbose(sprintf("%-15s %10d #","Sequences", $total_count));
$V->verbose(sprintf("%-15s %10d bp", "Total", $total_length));
$V->verbose(sprintf("%-15s %10d bp", "Longest", $total_longest));
$V->verbose(sprintf("%-15s %10d bp", "Shortest",$total_shortest));

my @total_Ns;
foreach (sort{$a <=> $b}keys %total_Ns){
	$V->verbose(sprintf("%-15s %10d bp", "N$_", $total_Ns{$_}));
	push @total_Ns, $total_Ns{$_};
}

$V->hline();

unless(@filtered_L){
	$V->verbose("No sequences found matching given filter criteria");
	exit(1);
}



$V->verbose("Filtered");
$V->verbose(sprintf("%-15s %10d #","Sequences", scalar @filtered_L));
$V->verbose(sprintf("%-15s %10d bp", "Total", $filtered_length));
$V->verbose(sprintf("%-15s %10d bp", "Longest", $filtered_longest));
$V->verbose(sprintf("%-15s %10d bp", "Shortest", $filtered_shortest));

my @filtered_Ns;
foreach (sort{$a <=> $b}keys %filtered_Ns){
	$V->verbose(sprintf("%-15s %10d bp", "N$_", $filtered_Ns{$_}));
	push @filtered_Ns, $filtered_Ns{$_};
}

# base content
my @BCs;
if(keys %BCs){
	foreach my $k(sort keys %BCs){
		$V->verbose(sprintf("%-15s %10d bp %10.2f %%", "[$k]", $BCs{$k}, (100*$BCs{$k}/$filtered_length)));
		push @BCs, $BCs{$k};
	}
}


$V->hline();
$V->nline();



if($tsv){
	print $tsv join("\t", scalar @filtered_L, $filtered_length, $filtered_longest, $filtered_shortest, @filtered_Ns, @BCs),"\n";
}



##------------------------------------------------------------------------##

=head1 Methods

=cut

=head2 opt2handle

Use this method with options accepting <LIST/FILE/-> to decide what was 
 actually provided and to return a handle to the content. 
=cut

sub opt2handle{
	my ($k, $v, $stdin) = @_;
	my $h;
	return unless $v; 
	
	if($v eq '-'){
		$V->exit("Cannot read $k from STDIN, already used with $stdin") if $stdin;
		$h = \*STDIN;
		$stdin = $k;
		$V->verbose("$k: STDIN");
	}elsif(-f $v){
		$V->verbose("$k: $v");
		open($h, '<', $v) or $V->exit("$!: $v");
	}else{
		open($h, '<', \$v) or $V->exit("$!: $v");;
	}
	return $h;	
}


# DEPRECATED -> opt2handle
#my %mf = (
#	'--ids' => \$opt_ids, 
#	'--ids_pattern' => \$opt_ids_pattern,
#	'--substr_file' => \$opt_substr_file
#);
#
#foreach(qw(--ids --ids_pattern --substr_file)){
#	next unless ${$mf{$_}}; 
#	if($mf{$_} eq '-'){
#		$V->exit("Cannot read $_ from STDIN, already used with $stdin") if $stdin;
#		${$mf{$_}} = \*STDIN;
#		$stdin = $_;
#		$V->verbose("$_: STDIN");
#	}elsif(-f ${$mf{$_}}){
#		$V->verbose("$_: ${$mf{$_}}");
#		open(my $fh, '<', $_);
#		${$mf{$_}} = $fh;
#	}else{
#		open(my $fh, '<', \$_);
#		${$mf{$_}} = $fh;
#	}
#}


=head2 read_ids

=cut

sub read_ids{
	my ($fh) = @_;
	my @IDS;
	while(<$fh>){
		next unless $_; # ignore empty lines
		next if /^#/; # ignore comments
		push @IDS, split(/[\s,\n]+/, $_);
	}
	map{s/^[>@]//}@IDS;
	my %IDS;
	@IDS{@IDS}=map{1}@IDS;
	return %IDS;
}



=head2 read_substrs

Read substr coordinate file and return 

=cut

sub read_substrs{
	my ($fh)=@_;
	my %s;
	while(<$fh>){
		chomp();
		next unless $_; # ignore empty lines
		next if /^#/; # ignore comments
		my ($id, @f) = split(/\s/, $_, 4);
		$id =~ s/^[>@]//;
		unless($opt_substr_perl){
			# transform from-to to offset-length
			if(@f==1){
				push @{$s{$id}{coords}}, [$f[0]];
				push @{$s{$id}{rc}}, 0;
			}elsif($f[0] > $f[1]){ # third param is rev-comp
				push @{$s{$id}{coords}}, [$f[1], $f[0]-$f[1]+1];
				push @{$s{$id}{rc}}, 1;
			}else{
				push @{$s{$id}{coords}}, [$f[0], $f[1]-$f[0]+1];
				push @{$s{$id}{rc}}, 0;
			}
		}else{
			push @{$s{$id}{coords}}, [@f];
			push @{$s{$id}{rc}}, 0;
		}
	}
	return %s;	
}

sub Nx{
	my $p = {
		total_length => undef,
		lengths => undef,
		Nxs => [@opt_N],
		@_
	};
	
	my %Ns;
	my @L = sort{$b <=> $a}@{$p->{lengths}};
	my @ls = map{$p->{total_length} * ($_/100)}@{$p->{Nxs}};
	
	my $lc = 0;
	foreach my $l (@L){
		$lc+= $l;
		if($lc >= $ls[0]){
			shift @ls;
			$Ns{shift @{$p->{Nxs}}} = $l;
			last unless @ls;
		}
	}
	return %Ns;
}	


sub ids_split_fh{
	my $match = shift;
	if(exists $IDS_SPLIT{$match}){
		if($IDS_SPLIT{$match}{open}){
			print "already open\n";
			return $IDS_SPLIT{$match}{fh}
		}else{ # reopen
			if($IDS_SPLIT_OPEN > 999){ # close one first
				my $k = each %IDS_SPLIT; # get a (random) key
				close $IDS_SPLIT{$k}{fh};
				$IDS_SPLIT{$k}{open} = 0;
				$IDS_SPLIT_OPEN--;
			}
			
			open(my $fh, '>>', ids_split_filename($match)) 
				or $V->exit("$!: ".ids_split_filename($match));
			$IDS_SPLIT{$match}{fh} = $fh;
			$IDS_SPLIT_OPEN++;
		}
	}else{
		print "new open\n";
		if($IDS_SPLIT_OPEN > 999){ # close one first
			my $k = each %IDS_SPLIT; # get a (random) key
			close {$IDS_SPLIT{$k}{fh}};
			$IDS_SPLIT{$k}{open} = 0;
			$IDS_SPLIT_OPEN--;
		}
		
		open(my $fh, '>', ids_split_filename($match)) 
			or $V->exit("$!: ".ids_split_filename($match));
		$IDS_SPLIT{$match}{fh} = $fh;
		$IDS_SPLIT{$match}{open}++;
		$IDS_SPLIT_OPEN++;
	}
	return $IDS_SPLIT{$match}{fh}
}


sub ids_split_filename{
	my $match = shift;
	return $IN_BASE.$match.$IN_SUFFIX;
}

=head1 AUTHOR

Thomas Hackl S<thomas.hackl@uni-wuerzburg.de>

=cut
